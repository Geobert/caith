number = @{ ASCII_DIGIT+ }
nb_dice = @{ ASCII_NONZERO_DIGIT+ ~ ASCII_DIGIT* }
op = _{ add | sub | mul | div }
add = { "+" }
sub = { "-" }
mul = { "*" }
div = { "/" }

dice = { nb_dice? ~ (roll ~ dice_side) ~ option* ~ target_failure{, 2} }
dice_side = _{ number | fudge }
fudge = { "F" | "f" }
roll = { "d" | "D" }
option = _{ explode | i_explode | reroll | i_reroll | keep_hi | keep_lo | drop_hi | drop_lo }
target_failure = _{ target | failure }
explode = { "e" ~ number }
i_explode = { ("ie" | "!") ~ number? }
reroll = { "r" ~ number }
i_reroll = { "ir" ~ number }
keep_hi = { "K" ~ number }
keep_lo = { "k" ~ number }
drop_hi = { "D" ~ number }
drop_lo = { "d" ~ number }
target =  { "t" ~ number }
failure =  { "f" ~ number }

repeated_expr = { "(" ~ expr ~ ")" ~ "^" ~ number }

expr = { leaf ~ (op ~ leaf)* }
leaf = _{ dice | number | "(" ~ expr ~ ")" }

reason = { ":" ~ ANY* }

command = _{ SOI ~ (repeated_expr | expr) ~ reason? ~ EOI }

WHITESPACE = _{ " " | "Â " }
